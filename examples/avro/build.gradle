plugins {
    id "com.github.imflog.kafka-schema-registry-gradle-plugin"
}
import com.github.imflog.schema.registry.Subject
import com.github.imflog.schema.registry.tasks.download.MetadataExtension

Subject locationAddressSubject = new Subject('location-address', 'schemas/avro/location-address.avsc', 'AVRO')
Subject locationCoordSubject = new Subject('location-latlong', 'schemas/avro/location-latlong.avsc', 'AVRO')

schemaRegistry {
    url = 'http://localhost:8081'
    quiet = false
    outputDirectory = 'schemas/avro/results/'
    pretty = true

    register {
        subject('company', 'schemas/avro/company.avsc', 'AVRO')
                .addLocalReference("Address", "schemas/avro/location-address.avsc")
        subject('user', 'schemas/avro/user.avsc', 'AVRO')
                .addReference('company', 'company', -1)
        subject(locationAddressSubject)
        subject(locationCoordSubject)
        subject("my-record","schemas/avro/my-record.avsc", "AVRO")
                .setMetadata("extra/metadata.json")
                .setRuleSet("extra/ruleSet.json")
                .setNormalized(true)
    }

    config {
        subject('company', 'FULL_TRANSITIVE')
        subject('user', 'FULL_TRANSITIVE')
        subject('location-address', 'FULL_TRANSITIVE')
        subject('location-latlong', 'FULL_TRANSITIVE')
    }

    download {
        metadata = new MetadataExtension(true)
//        subject('company', 'schemas/avro/downloaded') // is retrieved via reference from the user subject
        subject('user', 'schemas/avro/downloaded', true)
        subject('my-record', 'schemas/avro/downloaded', true)
        subjectPattern('location.*', 'schemas/avro/downloaded/location')
    }

    compatibility {
        subject('company', 'schemas/avro/company_v2.avsc', 'AVRO')
                .addLocalReference("Address", "schemas/avro/location-address.avsc")
        subject('user', 'schemas/avro/user.avsc', 'AVRO')
                .addReference('company', 'company', -1)
        subject(locationAddressSubject)
        subject(locationCoordSubject)
    }
}
