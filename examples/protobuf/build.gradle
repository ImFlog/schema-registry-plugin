plugins {
    id "com.github.imflog.kafka-schema-registry-gradle-plugin"
}

import com.github.imflog.schema.registry.Subject
import com.github.imflog.schema.registry.tasks.download.MetadataExtension

Subject locationAddressSubject = new Subject('location-address', 'schemas/protobuf/com/example/location-address.proto', 'PROTOBUF')
Subject locationCoordSubject = new Subject('location-latlong', 'schemas/protobuf/com/example/location-latlong.proto', 'PROTOBUF')

schemaRegistry {
    url = 'http://localhost:8081'
    quiet = false
    outputDirectory = 'schemas/protobuf/results/'

    register {
        subject('company', 'schemas/protobuf/com/example/company.proto', 'PROTOBUF')
        subject('user', 'schemas/protobuf/com/example/user.proto', 'PROTOBUF')
                .addReference('company', 'company', 1)
        subject(locationAddressSubject)
        subject(locationCoordSubject)
    }

    config {
        subject('company', 'FULL_TRANSITIVE')
        subject('user', 'FULL_TRANSITIVE')
        subject('location-address', 'FULL_TRANSITIVE')
        subject('location-latlong', 'FULL_TRANSITIVE')
    }

    download {
        metadata = new MetadataExtension(true)

        subject('company', 'schemas/protobuf/downloaded')
        subject('user', 'schemas/protobuf/downloaded')
        subjectPattern('location.*', 'schemas/protobuf/downloaded/location')
    }

    compatibility {
        subject('company', 'schemas/protobuf/com/example/company_v2.proto', 'PROTOBUF')
        subject('user', 'schemas/protobuf/com/example/user.proto', 'PROTOBUF')
                .addReference('company', 'company', 1)
        subject(locationAddressSubject)
        subject(locationCoordSubject)
    }
}
