plugins {
    id "com.github.imflog.kafka-schema-registry-gradle-plugin"
}

import com.github.imflog.schema.registry.Subject
import com.github.imflog.schema.registry.tasks.download.MetadataExtension

Subject locationAddressSubject = new Subject('location-address', 'schemas/json/location-address.json', 'JSON')
Subject locationCoordSubject = new Subject('location-latlong', 'schemas/json/location-latlong.json', 'JSON')

schemaRegistry {
    url = 'http://localhost:8081'
    quiet = false
    outputDirectory = 'schemas/json/results/'
    pretty = true

    register {
        subject('company', 'schemas/json/company.json', 'JSON')
            .addLocalReference('Address', 'schemas/json/location-address.json')
        subject('user', 'schemas/json/user.json', 'JSON')
            .addReference('company', 'company', -1)
        subject(locationAddressSubject)
        subject(locationCoordSubject)
    }

    config {
        subject('company', 'BACKWARD')
        subject('user', 'FULL_TRANSITIVE')
        subject('location-address', 'FULL_TRANSITIVE')
        subject('location-latlong', 'FULL_TRANSITIVE')
    }

    download {
        metadata = new MetadataExtension(true)
//        subject('company', 'schemas/json/downloaded') // Retrieved via reference from the user subject
        subject('user', 'schemas/json/downloaded', true)
        subjectPattern('location.*', 'schemas/json/downloaded/location')
    }

    compatibility {
        subject('company', 'schemas/json/company_v2.json', 'JSON')
            .addLocalReference("address", "schemas/json/location-address.json")
        subject('user', 'schemas/json/user.json', 'JSON')
            .addReference('company', 'company', -1)
        subject(locationAddressSubject)
        subject(locationCoordSubject)
    }
}
